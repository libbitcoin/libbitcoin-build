.template 0
###############################################################################
# Copyright (c) 2014-2015 libbitcoin developers (see COPYING).
#
# GSL generate libbitcoin travis.yml.
#
# This is a code generator built using the iMatix GSL code generation
# language. See https://github.com/imatix/gsl for details.
###############################################################################
# Functions
###############################################################################

function apt_get(name, system)
    define my.get = ""
    if (my.system = "osx")
        my.get = "brew install $(my.name)"
    elsif (my.system = "linux")
    else
        abort "Invalid system type for get package: $(my.name)."
    endif
    return my.get
endfunction

# We must do this because lcov is uses exclusions only.
function job_coverage_exclusions(job, repository)
    define my.job = job_coverage_exclusions.job
    define my.repository = job_coverage_exclusions.repository

    # Exclude all system directories.
    define my.excludes = quote("/usr/*")

    # Exclude the prefix directory (absolute).
    define my.prefix = job_option(my.job, "prefix")?
    if (!is_empty(my.prefix))
        my.excludes += " $(quote("$(my.prefix)/*"))"
    endif

    # Exclude the build-dir directory (relative to main target).
    define my.build_dir = job_option(my.job, "build-dir")?
    if (is_empty(my.build_dir))
        define my.library = my.repository.name
        my.excludes += " $(quote("build-$(my.library)/*"))"
    else
        my.excludes += " $(quote("$(my.build_dir)/*"))"
    endif

    # Exclude directories of cpp products that are not installed (relative).
    define my.make = my.repository->make?
    if (defined(my.make))
        for my.make.product as _product where is_not_covered(_product)
            my.excludes += " $(quote("$(_product.path)/*"))"
        endfor _product
    endif

    # Exclude explicitly-specified paths.
    for my.job.exclude as _exclude
        my.excludes += " $(quote(_exclude.path))"
    endfor _exclude

    return my.excludes
endfunction

function job_options(job)
    define my.job = job_options.job
    define my.job_options = ""
    for my.job.option as _option
        my.job_options += " $(_option.value)"
    endfor _option
    return trim_left(my.job_options)
endfunction

function job_install(job, restrict_parallel)
    define my.job = job_install.job
    define my.parallel = job_install.restrict_parallel ?? "PARALLEL=1 " ? ""
    define my.sudo = is_true(my.job.sudo) ?? "sudo " ? ""
    define my.compiler = "CC=$CC CXX=$CXX "
    return "$(my.sudo)$(my.compiler)$(my.parallel)./install.sh $(job_options(my.job))"
endfunction

function job_predicate_link(job)
    define my.job = job_predicate_link.job
    return "$$(my.job.link:upper,c%-7s)"
endfunction

function job_predicate_system(job)
    define my.job = job_predicate.job
    return "$$(my.job.system:upper,c%-5s)"
endfunction

function job_predicate_compiler(job)
    define my.job = job_predicate_compiler.job
    return "$$(my.job.compiler:upper,c%-5s)"
endfunction

function job_predicate_platform(job)
    define my.job = job_predicate_platform.job
    return "$(job_predicate_system(my.job)) && $(job_predicate_compiler(my.job))"
endfunction

function job_predicate(job)
    define my.job = job_predicate.job
    return "$(job_predicate_platform(my.job)) && $(job_predicate_link(my.job))"
endfunction

function condition(predicate)
    return "if [[ $(my.predicate) ]]; then"
endfunction

function is_system_linux(job)
    define my.job = is_system_linux.job
    return defined(my.job) & my.job.system = "linux"
endfunction

function is_system_osx(job)
    define my.job = is_system_osx.job
    return defined(my.job) & my.job.system = "osx"
endfunction

function is_travis_job(job)
    define my.job = is_travis_job.job
    return is_system_linux(my.job) | is_system_osx(my.job)
endfunction

###############################################################################
# Macros
###############################################################################
.endtemplate
.template 1
.
.macro initialize_job(job)
.   define my.job = initialize_job.job
.   define my.is_clang = (my.job.compiler = "clang")
        - os: $(my.job.system)
          compiler: $(my.job.compiler)
          env: LINK=$(my.job.link)
.   if (my.job.system = "linux")
.       if ((my.is_clang | is_coverage_job(my.job) | count(my.job.get) > 0))
          addons:
            apt:
.           if (my.is_clang)
              sources:
                - sourceline: 'ppa:h-rayflood/llvm'
.           endif
.           if (my.is_clang | is_coverage_job(my.job) | count(my.job.get) > 0)
              packages:
.           endif
.           if (my.is_clang)
                - clang-3.4
.           endif
.           if (is_coverage_job(my.job))
                - lcov
.           endif
.           for my.job.get as _get
                - $(_get.name)
.           endfor _get
.       endif
.   endif
.endmacro #initialize_job
.
.macro initialize_matrix(matrix)
.   define my.matrix = initialize_matrix.matrix
addons:
    apt:
        sources:
            - boost-latest
            - ubuntu-toolchain-r-test
        packages:
            - g++-4.8

matrix:
    include:
.   for my.matrix.job by "$(compiler)-$(link)" as _job where is_system_osx(_job)
.       initialize_job(_job)
.   endfor _job
.   for my.matrix.job by "$(compiler)-$(link)" as _job where is_system_linux(_job)
.       initialize_job(_job)
.   endfor _job
    fast_finish:
        - true

.endmacro #initialize_matrix
.
.macro set_aliases(matrix)
.   define my.matrix = set_aliases.matrix
    # Set aliases for readability.
    - $(condition("$CC             == gcc    ")) GCC=true; fi
    - $(condition("$CC             == clang  ")) CLANG=true; fi
    - $(condition("$LINK           == static ")) STATIC=true; fi
    - $(condition("$LINK           == dynamic")) DYNAMIC=true; fi
    - $(condition("$TRAVIS_OS_NAME == osx    ")) OSX=true; fi
    - $(condition("$TRAVIS_OS_NAME == linux  ")) LINUX=true; fi

.endmacro #set_aliases
.
.macro update_environment(matrix)
.   define my.matrix = update_environment.matrix
    # Update homebrew.
    - if [[ $OSX   && $CLANG             ]]; then brew update; fi

.endmacro #update_environment
.
.# The --build-dir install.sh option is not supported by this code coverage tool.
.macro code_coverage(repository)
.   define my.repository = code_coverage.repository
.   if (count_matrix_coverage_job(my.repository->matrix) > 0)
    # Install coveralls and current lcov.
.      for my.repository->matrix.job as _job where is_coverage_job(_job)
.           define my.job_condition = condition(job_predicate(_job))
    - $(my.job_condition) gem install coveralls-lcov; fi
.      endfor

    # Capture coverage info, filter out system and test code, and debug before upload.
.      for my.repository->matrix.job as _job where is_coverage_job(_job)
.           define my.job_condition = condition(job_predicate(_job))
.           define my.exclusions = job_coverage_exclusions(_job, my.repository)
    - $(my.job_condition) lcov --directory . --capture --output-file coverage.info; fi
    - $(my.job_condition) lcov --remove coverage.info $(my.exclusions) --output-file coverage.info; fi
    - $(my.job_condition) lcov --list coverage.info; fi
.       endfor

    # Upload coverage info to coveralls service (--repo-token <private coveralls repo token>).
.      for my.repository->matrix.job as _job where is_coverage_job(_job)
.           define my.job_condition = condition(job_predicate(_job))
    - $(my.job_condition) coveralls-lcov coverage.info; fi
.       endfor
.   endif

.endmacro #code_coverage
.
.macro install_packages(matrix)
.   define my.matrix = install_packages.matrix
    # Export CC/CXX to control gcc version.
    - if [[ $GCC                         ]]; then export CC=gcc-4.8; export CXX=g++-4.8; fi

    # Export CC/CXX to control clang version.
    - if [[ $CLANG                       ]]; then export CC=clang-3.4; export CXX=clang++-3.4; fi

    # Download and install packages.
.   for my.matrix.job as _job where is_travis_job(_job) & count(_job.get) > 0
.       for _job.get as _get
.           if (_job.system = "osx")
    - $(condition(job_predicate(_job))) $(apt_get(_get.name, _job.system)); fi
.           elsif (_job.system = "linux")
.           else
.               abort "Invalid system type for get package: $(my.name)."
.           endif
.       endfor _get
.   endfor _job

.endmacro #install_packages
.
.macro run_script(matrix)
.   define my.matrix = run_script.matrix
.   define my.restrict_parallel = is_true(my.matrix->job.sudo)
    # Download and build $(_repository.name) and all dependencies.
.   for my.matrix.job as _job where is_travis_job(_job)
    - $(condition(job_predicate(_job))) $(job_install(_job, my.restrict_parallel)); fi
.   endfor _job

.endmacro #run_script
.
.endtemplate
.template 0
###############################################################################
# Generation
###############################################################################
.endtemplate
.template 1
.macro generate_travis_yml()
.for generate.repository by name as _repository
.   require(_repository, "repository", "name")
.   create_directory(_repository.name)
.   define my.out_file = "$(_repository.name)/.travis.yml"
.   notify(my.out_file)
.   output(my.out_file)
.   copyleft(_repository.name)
.   define my.matrix = _repository->matrix
.   define my.sudo = is_true(my.matrix->job.sudo) ?? "true" ? "false"
sudo:
    - $(my.sudo)

language:
    - cpp

.   initialize_matrix(my.matrix)
before_install:

.   set_aliases(my.matrix)
.   update_environment(my.matrix)
install:

.   install_packages(my.matrix)
script:

.   run_script(my.matrix)
after_success:

.   code_coverage(_repository)
.
.   close
.endfor _repository
.endmacro # generate_travis_yml
.endtemplate
